% PROBLEM 
% Color a circuit of n vertices with 4 colors, such that neighbors have different colors
% PWs depending on n = 2,3,4,5,6,... is 12,24,84,240,732, ...
% In [Proof of Lemma 2.6, steinberger2009unavoidable] mentions that for a
% 4-circuit (1->2->3->4->1) there are four equivalence classes only:
%  {rgby, rgry, rgbg, rgrg} so the 84 colorings fall into 4 "types" or classes.

% Vertices (nodes, regions) of the circuit
#const n=4.
v(1..n).

% Neighborhood relation of a circuit:
n(I,I+1) :- v(I), I < n. % Build a chain .. 
n(n,1).                  % .. and close the circuit
n(Y,X) :- n(X,Y).        % symmetric closure of n/2 (not really needed here)

% Let there be exactly 4 colors:
colors(r;g;b;y).

% Assign exactly one color to each vertex:
1 {col(X, C) : colors(C) } 1 :- v(X). 

% Two neighbors X and Y must not have the same color!
:- col(X, C), col(Y, C), n(X,Y).

% Determine the coloring vector:
t(C1,C2,C3,C4) :- col(1,C1), col(2,C2), col(3,C3), col(4,C4).

% Determine which equivalence classes ("Klassen") exists: 
% (this could be done via different sets of inequalities X != Y, Y != Z, .. )
k(rgrg) :- t(X,Y,X,Y).               % 2-color pattern: equalities 1=3, 2=4
k(rgry) :- t(X,Y,X,U), not k(rgrg).  % 3-color pattern: equality 1=3
k(rgbg) :- t(X,Y,Z,Y), not k(rgrg).  % 3-color pattern: equality 2=4
k(rgby) :- t(X,Y,Z,U), not k(rgry), not k(rgbg), not k(rgrg).  % 4-color pattern: no equalities

% #show t/4.  % this gives 84 answers (all colorings)
#show k/1.    % this gives 4 answers (all equivalence classes)

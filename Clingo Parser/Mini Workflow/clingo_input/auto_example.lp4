%RCC RULES:

eq(X,Y) :- eq(X,Z), eq(Z,Y).
dr(X,Y) :- eq(X,Z), dr(Z,Y).
pp(X,Y) :- eq(X,Z), pp(Z,Y).
pi(X,Y) :- eq(X,Z), pi(Z,Y).
po(X,Y) :- eq(X,Z), po(Z,Y).

dr(X,Y) :- dr(X,Z), eq(Z,Y).
1 {dr(X,Y) ; pp(X,Y) ; po(X,Y)} :- dr(X,Z), pp(Z,Y).
1 {dr(X,Y) ; pi(X,Y) ; po(X,Y)} :- dr(X,Z), pi(Z,Y).
1 {dr(X,Y) ; pp(X,Y) ; po(X,Y)} :- dr(X,Z), po(Z,Y).

pp(X,Y) :- pp(X,Z), eq(Z,Y).
dr(X,Y) :- pp(X,Z), dr(Z,Y).
pp(X,Y) :- pp(X,Z), pp(Z,Y).
dr(X,Y) ; pp(X,Y) ; po(X,Y) :- pp(X,Z), po(Z,Y).

pi(X,Y) :- pi(X,Z), eq(Z,Y).
1 {dr(X,Y) ; pi(X,Y) ; po(X,Y)} :- pi(X,Z), dr(Z,Y).
pi(X,Y) :- pi(X,Z), pi(Z,Y).
1 {pi(X,Y) ; po(X,Y)} :- pi(X,Z), po(Z,Y).

po(X,Y) :- po(X,Z), eq(Z,Y).
1 {dr(X,Y) ; pi(X,Y) ; po(X,Y)} :- po(X,Z), dr(Z,Y).
1 {pp(X,Y) ; po(X,Y)} :- po(X,Z), pp(Z,Y).
1 {dr(X,Y) ; pi(X,Y) ; po(X,Y)} :- po(X,Z), pi(Z,Y).

u(X) :- dr(X,_).
u(X) :- dr(_,X).
u(X) :- eq(X,_).
u(X) :- eq(_,X).
u(X) :- po(X,_).
u(X) :- po(_,X).
u(X) :- pp(X,_).
u(X) :- pp(_,X).
u(X) :- pi(X,_).
u(X) :- pi(_,X).
u(X) :- bl(_,X).
u(X) :- bl(X,_).

eq(X,X) :- u(X).
pi(X,Y) :- pp(Y,X).
pp(X,Y) :- pi(Y,X).
eq(X,Y) :- eq(Y,X).
po(X,Y) :- po(Y,X).
dr(X,Y) :- dr(Y,X).
dr(Y,Z) :- bl(X,Y), bl(X,Z), Y != Z.
pp(Y,X) :- bl(X,Y).

1 {eq(X,Y) ; dr(X,Y) ; pp(X,Y) ; pi(X,Y) ; po(X,Y)} :- u(X), u(Y), X != Y.

:- eq(X,Y), dr(X,Y).
:- eq(X,Y), pp(X,Y).
:- eq(X,Y), pi(X,Y).
:- eq(X,Y), po(X,Y).
:- dr(X,Y), pp(X,Y).
:- dr(X,Y), pi(X,Y).
:- dr(X,Y), po(X,Y).
:- pp(X,Y), pi(X,Y).
:- pp(X,Y), po(X,Y).
:- pi(X,Y), po(X,Y).

%TAXONOMY DESC RULES

bl("T1_Automobile","T1_Sedan").
bl("T1_Automobile","T1_Wagon").
bl("T1_Automobile","T1_SUV").
bl("T1_Sedan","T1_Sedan_Audi").
bl("T1_Sedan","T1_Sedan_BMW").
bl("T1_Wagon","T1_Wagon_Audi").
bl("T1_Wagon","T1_Wagon_BMW").
bl("T2_Automobile","T2_Audi").
bl("T2_Automobile","T2_BMW").
bl("T2_Audi","T2_Sedan_Audi").
bl("T2_Audi","T2_Wagon_Audi").
bl("T2_Audi","T2_SUV_Audi").
bl("T2_BMW","T2_Sedan_BMW").
bl("T2_BMW","T2_SUV_BMW").
eq("T1_Automobile","T2_Automobile").
eq("T1_Sedan_BMW","T2_Sedan_BMW").
eq("T1_Wagon_Audi","T2_Wagon_Audi").
pp("T1_Wagon_BMW","T2_BMW").